buildscript {
    ext.mm_kotlinVersion = "2.0.21"
    ext.mm_gradlePluginVersion = "8.5.2"
    ext.mm_agcpVersion = "1.9.1.303"

    repositories {
        google()
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$mm_gradlePluginVersion"
        classpath "com.huawei.agconnect:agcp:$mm_agcpVersion"
    }
}

plugins {
    id 'com.android.application' version "$mm_gradlePluginVersion" apply false
    id 'com.android.library' version "$mm_gradlePluginVersion" apply false
    id 'net.researchgate.release' version '3.0.2'
    id 'maven-publish'
    id "io.github.gradle-nexus.publish-plugin" version "1.1.0"
    id "org.jetbrains.kotlin.android" version "$mm_kotlinVersion" apply false
    id "org.sonarqube" version "4.4.1.3373"
    id "jacoco"
    id "java"
}

ext {
    mm_minSdkVersion = 21
    mm_compileSdkVersion = 35
    mm_targetSdkVersion = 35
    mm_buildToolsVersion = "35.0.0"
    mm_androidSdkVersion = "13.17.0"
    mm_constraintLayoutVersion = "2.1.4"
    mm_appCompatVersion = "1.6.1"
    mm_materialVersion = "1.12.0"
    mm_coreKtxVersion = "1.10.1"
    mm_lifecycleVersion = "2.5.1"
    mm_desugarJdkVersion = "2.0.3"
    mm_jUnitVersion = "4.12"
    mm_androidXjUnitVersion = "1.2.1"
    mm_androidTestRunnerVersion = "1.6.2"
    mm_mockitoCoreVersion = "1.10.19"
    mm_coroutinesVersion = "1.9.0"
    mm_hmsPushVersion = "6.7.0.300"
}

//It's required to be applied here, to have `subprojects.publish` task available
subprojects {
    apply plugin: "maven-publish"
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }
    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            project.android {
                if (namespace == null) {
                    namespace project.group
                }
            }
        }
    }
}

tasks.register('publishAllPublicationsToMavenRepository') {
    dependsOn subprojects.collect { it.tasks.named('publishReleasePublicationToSonatypeRepository') }
}

tasks.register('updateVersionInReadme') {
    doLast {
        def readmeFile = new File('README.md')
        def text = readmeFile.text
        readmeFile.withWriter { w ->
            w << text.replaceFirst('infobip-mobile-messaging-huawei-sdk:\\d+\\.\\d+\\.\\S+@aar', "infobip-mobile-messaging-huawei-sdk:$version@aar")
        }
    }
}

afterReleaseBuild.dependsOn updateVersionInReadme

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            username = project.hasProperty('ossrhUsername') ? project.property('ossrhUsername') : System.getenv('OSSRH_USERNAME')
            password = project.hasProperty('ossrhPassword') ? project.property('ossrhPassword') : System.getenv('OSSRH_PASSWORD')
            stagingProfileId.set(project.hasProperty('sonatypeStagingProfileId') ? project.property('sonatypeStagingProfileId') : System.getenv('SONATYPE_STAGING_PROFILE_ID'))
        }
    }
    connectTimeout = Duration.ofMinutes(3)
    clientTimeout = Duration.ofMinutes(3)
}

closeAndReleaseSonatypeStagingRepository.mustRunAfter subprojects.publish
afterReleaseBuild.dependsOn subprojects.publish, closeAndReleaseSonatypeStagingRepository

release {
    buildTasks = ['clean', 'build']
    git {
        requireBranch.set('')
        pushToRemote.set('origin')
    }
}

project.tasks.named("sonarqube").configure {
    dependsOn 'lint'
    dependsOn 'lintReportRelease'
}

sonar {
    properties {
        property "sonar.host.url", "https://sonar.ib-ci.com"
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.branch.name", System.getenv('BRANCH_NAME_TO_BUILD')
        property "sonar.projectVersion", project.version
        property "sonar.projectName", "infobip-mobile-messaging-huawei"
        property "sonar.projectKey" , "infobip-mobile-messaging-huawei"
        property "sonar.androidLint.reportPaths", "${layout.buildDirectory}/reports/lint-results.xml"
        property "sonar.exclusions", "" +
                "infobip-mobile-messaging-huawei-demo/**/*, " +
                "infobip-mobile-messaging-huawei-cryptor-migration/**/*, " +
                "infobip-mobile-messaging-huawei-test/**/*, "
        property "sonar.java.coveragePlugin", "jacoco"
        property "sonar.junit.reportsPath", "${layout.buildDirectory}/test-results/test/"
        property "sonar.coverage.jacoco.xmlReportPaths", "" +
                "${project.projectDir}/infobip-mobile-messaging-huawei-inbox-sdk/build/reports/jacoco/debugCoverage/debugCoverage.xml, " +
                "${project.projectDir}/infobip-mobile-messaging-huawei-sdk/build/reports/jacoco/debugCoverage/debugCoverage.xml, "
    }
}