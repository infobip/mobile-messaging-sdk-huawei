buildscript {
    ext.mm_kotlinVersion = "2.0.21"
    ext.mm_gradlePluginVersion = '8.9.2'
    ext.mm_agcpVersion = "1.9.1.303"

    repositories {
        google()
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$mm_gradlePluginVersion"
        classpath "com.huawei.agconnect:agcp:$mm_agcpVersion"
    }
}

plugins {
    id 'com.android.application' version "$mm_gradlePluginVersion" apply false
    id 'com.android.library' version "$mm_gradlePluginVersion" apply false
    id 'net.researchgate.release' version '3.0.2'
    id 'maven-publish'
    id "io.github.gradle-nexus.publish-plugin" version "1.1.0"
    id "org.jetbrains.kotlin.android" version "$mm_kotlinVersion" apply false
    id "org.sonarqube" version "4.4.1.3373"
    id "jacoco"
    id "java"
    id 'com.diffplug.spotless' version '8.0.0' apply false
}

ext {
    mm_minSdkVersion = 21
    mm_compileSdkVersion = 36
    mm_targetSdkVersion = 36
    mm_buildToolsVersion = "36.0.0"
    mm_androidSdkVersion = "14.9.2"
    mm_constraintLayoutVersion = "2.2.1"
    mm_appCompatVersion = "1.7.1"
    mm_activityVersion = "1.11.0"
    mm_materialVersion = "1.13.0"
    mm_coreKtxVersion = "1.16.0"
    mm_lifecycleVersion = "2.9.3"
    mm_desugarJdkVersion = "2.0.3"
    mm_jUnitVersion = "4.12"
    mm_androidXjUnitVersion = "1.2.1"
    mm_androidTestRunnerVersion = "1.6.2"
    mm_mockitoCoreVersion = "4.11.0"
    mm_coroutinesVersion = "1.10.2"
    mm_hmsPushVersion = "6.7.0.300"
}

//It's required to be applied here, to have `subprojects.publish` task available
subprojects {
    apply plugin: "maven-publish"
    apply plugin: 'com.diffplug.spotless'

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }
    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            project.android {
                if (namespace == null) {
                    namespace project.group
                }
            }
        }
    }

    // Spotless configuration for copyright headers
    spotless {
        java {
            target 'src/**/*.java'
            targetExclude '**/build/**', '**/generated/**', '**/DebugServer.java'
            licenseHeaderFile rootProject.file('spotless/copyright-header.txt')
        }

        kotlin {
            target 'src/**/*.kt'
            targetExclude '**/build/**', '**/generated/**'
            licenseHeaderFile rootProject.file('spotless/copyright-header.txt')
        }

        format 'xml', {
            target 'src/**/*.xml'
            targetExclude '**/build/**', '**/generated/**'
            //<[a-zA-Z] matching the beginning of the XML root tag, everything before that will be replaced with the license header
            licenseHeaderFile rootProject.file('spotless/copyright-header-xml.txt'),  '<[a-zA-Z]'
        }
    }

    /*
     * 1. build task → depends on check task
     * 2. check task → depends on spotlessCheck task (and test)
     * 3. spotlessCheck → validates code formatting
     *
     * So when you run ./gradlew build, it will:
     * - Run tests
     * - Run Spotless formatting checks
     * - Build the project
     *
     * If Spotless finds any formatting violations or missing copyright, the build will fail.
     */
    tasks.named('check') {
        dependsOn 'spotlessCheck'
    }
}

tasks.register('publishAllPublicationsToMavenRepository') {
    dependsOn subprojects.collect { it.tasks.named('publishReleasePublicationToSonatypeRepository') }
}

tasks.register('updateVersionInReadme') {
    doLast {
        def readmeFile = new File('README.md')
        def text = readmeFile.text
        readmeFile.withWriter { w ->
            w << text.replaceFirst('infobip-mobile-messaging-huawei-sdk:\\d+\\.\\d+\\.\\S+@aar', "infobip-mobile-messaging-huawei-sdk:$version@aar")
        }
    }
}

afterReleaseBuild.dependsOn updateVersionInReadme

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://ossrh-staging-api.central.sonatype.com/service/local/"))
            snapshotRepositoryUrl.set(uri("https://central.sonatype.com/repository/maven-snapshots/"))
            username = project.hasProperty('ossrhUsername') ? project.property('ossrhUsername') : System.getenv('OSSRH_USERNAME')
            password = project.hasProperty('ossrhPassword') ? project.property('ossrhPassword') : System.getenv('OSSRH_PASSWORD')
            stagingProfileId.set(project.hasProperty('sonatypeStagingProfileId') ? project.property('sonatypeStagingProfileId') : System.getenv('SONATYPE_STAGING_PROFILE_ID'))
        }
    }
    connectTimeout = Duration.ofMinutes(3)
    clientTimeout = Duration.ofMinutes(3)
}

closeAndReleaseSonatypeStagingRepository.mustRunAfter subprojects.publish
afterReleaseBuild.dependsOn subprojects.publish, closeAndReleaseSonatypeStagingRepository

release {
    buildTasks = ['clean', 'build']
    git {
        requireBranch.set('')
        pushToRemote.set('origin')
    }
}

project.tasks.named("sonarqube").configure {
    dependsOn 'lint'
    dependsOn 'lintReportRelease'
}

sonar {
    properties {
        property "sonar.host.url", "https://sonar.ib-ci.com"
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.branch.name", System.getenv('BRANCH_NAME_TO_BUILD')
        property "sonar.projectVersion", project.version
        property "sonar.projectName", "infobip-mobile-messaging-huawei"
        property "sonar.projectKey" , "infobip-mobile-messaging-huawei"
        property "sonar.androidLint.reportPaths", "${layout.buildDirectory}/reports/lint-results.xml"
        property "sonar.exclusions", "" +
                "infobip-mobile-messaging-huawei-demo/**/*, " +
                "infobip-mobile-messaging-huawei-cryptor-migration/**/*, " +
                "infobip-mobile-messaging-huawei-test/**/*, "
        property "sonar.java.coveragePlugin", "jacoco"
        property "sonar.junit.reportsPath", "${layout.buildDirectory}/test-results/test/"
        property "sonar.coverage.jacoco.xmlReportPaths", "" +
                "${project.projectDir}/infobip-mobile-messaging-huawei-inbox-sdk/build/reports/jacoco/debugCoverage/debugCoverage.xml, " +
                "${project.projectDir}/infobip-mobile-messaging-huawei-sdk/build/reports/jacoco/debugCoverage/debugCoverage.xml, "
    }
}