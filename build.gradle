buildscript {

    repositories {
        maven {
            url 'https://maven.google.com/'
            name 'Google'
        }
        mavenCentral()
        google()
        // huawei maven
        maven { url 'http://developer.huawei.com/repo/' }

    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.6.2'
    }
}

plugins {
    id 'net.researchgate.release' version '2.8.1'
    id 'maven-publish'
    id "io.github.gradle-nexus.publish-plugin" version "1.1.0"
}

ext {
    mm_minSdkVersion = 17
    mm_compileSdkVersion = 28
    mm_targetSdkVersion = 28
    mm_buildToolsVersion = "28.0.3"
    mm_supportLibrariesVersion = "28.0.0"
    mm_apiVersion = "3.0.4"
}

allprojects {
    repositories {
        google()
        jcenter()
        mavenCentral()
        maven {
            url 'https://maven.google.com/'
            name 'Google'
        }
        // huawei maven
        maven { url 'http://developer.huawei.com/repo/' }
    }
}

subprojects {
    apply plugin: "maven"
    apply plugin: "maven-publish"
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task updateVersionInReadme {
    doLast {
        def readmeFile = new File('README.md')
        def text = readmeFile.text
        readmeFile.withWriter { w ->
            w << text.replaceFirst('infobip-mobile-messaging-huawei-sdk:\\d+\\.\\d+\\.\\S+@aar', "infobip-mobile-messaging-huawei-sdk:$version@aar")
        }
    }
}

afterReleaseBuild.dependsOn updateVersionInReadme

// workaround for `net.researchgate.release` plugin issue:
// https://github.com/researchgate/gradle-release/issues/186
task build {}
build.dependsOn subprojects.findAll {!it.name.contains("demo")}.build

afterReleaseBuild.dependsOn subprojects.publish

release {
    buildTasks = ['clean', 'build']
    git {
        requireBranch = ''
        pushToRemote = 'origin'
    }
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            username = project.hasProperty('ossrhUsername') ? project.property('ossrhUsername') : System.getenv('OSSRH_USERNAME')
            password = project.hasProperty('ossrhPassword') ? project.property('ossrhPassword') : System.getenv('OSSRH_PASSWORD')
            stagingProfileId.set(project.hasProperty('sonatypeStagingProfileId') ? project.property('sonatypeStagingProfileId') : System.getenv('SONATYPE_STAGING_PROFILE_ID'))
        }
    }
    connectTimeout = Duration.ofMinutes(3)
    clientTimeout = Duration.ofMinutes(3)
}
