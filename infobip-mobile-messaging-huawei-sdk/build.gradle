plugins {
    id 'com.android.library'
    id 'com.huawei.agconnect'
    id 'maven-publish'
    id 'idea'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.kapt'
}

android {
    publishing {
        multipleVariants {
            allVariants()
            withJavadocJar()
            withSourcesJar()
        }
    }
    namespace = "org.infobip.mobile.messaging"
    buildToolsVersion = mm_buildToolsVersion
    compileSdk = mm_compileSdkVersion

    defaultConfig {
        minSdkVersion mm_minSdkVersion
        targetSdkVersion mm_targetSdkVersion

        // since AGP 4.1 "VERSION_NAME" is not included anymore to BuildConfig
        // https://issuetracker.google.com/issues/154275579
        buildConfigField "String", "VERSION_NAME", "\"$versionName\""
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        consumerProguardFiles 'proguard-rules.pro'
    }

    buildFeatures {
        buildConfig true
    }

    buildTypes {
        debug {
            enableUnitTestCoverage true
            enableAndroidTestCoverage true
        }
    }

    lint {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    api "com.infobip:infobip-mobile-messaging-api-java:${mm_androidSdkVersion}"
    api "com.infobip:infobip-mobile-messaging-android-resources:${mm_androidSdkVersion}@aar"
    api 'androidx.legacy:legacy-support-v4:1.0.0'
    api "androidx.appcompat:appcompat:$mm_appCompatVersion"

    // push kit
    implementation "com.huawei.hms:push:$mm_hmsPushVersion"

    implementation('androidx.cardview:cardview:1.0.0')
    implementation 'androidx.work:work-runtime:2.10.0'

    androidTestImplementation project(':infobip-mobile-messaging-huawei-test')
    androidTestImplementation "androidx.test.ext:junit:$mm_androidXjUnitVersion"
    androidTestImplementation "junit:junit:$mm_jUnitVersion"
    androidTestImplementation "org.mockito:mockito-core:$mm_mockitoCoreVersion"
    androidTestImplementation 'com.nanohttpd:nanohttpd:2.1.0'
    androidTestImplementation('org.skyscreamer:jsonassert:1.5.0') {
        exclude group: "org.json", module: "json"
    }

    testImplementation project(':infobip-mobile-messaging-huawei-test')
    testImplementation "junit:junit:$mm_jUnitVersion"

    implementation('com.google.code.gson:gson:2.9.0')
}

android.libraryVariants.all { variant ->
    def name = variant.buildType.name
    def jar = project.tasks.create(name: "jar${name.capitalize()}", type: Jar) {
        Task javaCompileTask
        if (variant.hasProperty('javaCompileProvider')) {
            // Android 3.3.0+
            javaCompileTask = variant.javaCompileProvider.get()
        } else {
            javaCompileTask = variant.javaCompile
        }

        dependsOn javaCompileTask
        from javaCompileTask.destinationDirectory

        manifest {
            attributes(
                    "Bundle-Name": 'parse-android',
                    "Bundle-Version": project.version
            )
        }

        exclude '**/R.class'
        exclude '**/R\$*.class'
        exclude '**/Manifest.class'
        exclude '**/Manifest\$*.class'
        exclude '**/BuildConfig.class'
    }

    artifacts {
        archives jar
    }
}

ext {
    PUBLISH_ARTIFACT_ID = 'infobip-mobile-messaging-huawei-sdk'
}
apply from: "${rootProject.projectDir}/publish-mavencentral.gradle"
apply from: "${rootProject.projectDir}/jacoco.gradle"