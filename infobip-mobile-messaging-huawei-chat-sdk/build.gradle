buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$mm_gradlePluginVersion"
    }
}

plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.kapt'
}

android {
    namespace = "org.infobip.mobile.messaging.chat"
    buildToolsVersion = mm_buildToolsVersion
    compileSdk = mm_compileSdkVersion

    defaultConfig {
        minSdkVersion mm_minSdkVersion
        targetSdkVersion mm_targetSdkVersion
        versionCode 1
        versionName project.version

        // since AGP 4.1 "VERSION_NAME" is not included anymore to BuildConfig
        // https://issuetracker.google.com/issues/154275579
        buildConfigField "String", "VERSION_NAME", "\"$versionName\""
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        consumerProguardFiles 'proguard-rules.pro'
        vectorDrawables.useSupportLibrary = true
    }

    buildFeatures {
        viewBinding true
    }

    buildTypes {
        debug {
            testCoverageEnabled true
            //needed for androidTests
            multiDexEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation project(':infobip-mobile-messaging-huawei-sdk')
    api ("com.infobip:infobip-mobile-messaging-android-resources:${mm_androidSdkVersion}@aar")
    implementation "com.infobip:infobip-mobile-messaging-api-java:${mm_androidSdkVersion}"

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$mm_kotlinVersion"
    implementation "androidx.core:core-ktx:$mm_coreKtxVersion"

    implementation "androidx.constraintlayout:constraintlayout:$mm_constraintLayoutVersion"
    implementation "androidx.appcompat:appcompat:$mm_appCompatVersion"
    implementation "com.google.android.material:material:$mm_materialVersion"
    implementation 'androidx.exifinterface:exifinterface:1.3.6'

    androidTestImplementation project(":infobip-mobile-messaging-huawei-test")
    androidTestImplementation "androidx.test.ext:junit:$mm_androidXjUnitVersion"
    androidTestImplementation "junit:junit:$mm_jUnitVersion"
    androidTestImplementation "org.mockito:mockito-core:$mm_mockitoCoreVersion"
    androidTestImplementation 'com.nanohttpd:nanohttpd:2.1.0'
    androidTestImplementation 'com.android.support:multidex:2.0.1'

    testImplementation project(":infobip-mobile-messaging-huawei-test")
    testImplementation "junit:junit:$mm_jUnitVersion"
    testImplementation "org.mockito:mockito-core:$mm_mockitoCoreVersion"

}

android.libraryVariants.all { variant ->
    def name = variant.buildType.name
    def jar = project.tasks.create(name: "jar${name.capitalize()}", type: Jar) {
        Task javaCompileTask
        if (variant.hasProperty('javaCompileProvider')) {
            // Android 3.3.0+
            javaCompileTask = variant.javaCompileProvider.get()
        } else {
            javaCompileTask = variant.javaCompile
        }

        dependsOn javaCompileTask
        from javaCompileTask.destinationDir

        manifest {
            attributes(
                    "Bundle-Name": 'parse-android',
                    "Bundle-Version": project.version
            )
        }

        exclude '**/R.class'
        exclude '**/R\$*.class'
        exclude '**/Manifest.class'
        exclude '**/Manifest\$*.class'
        exclude '**/BuildConfig.class'
    }

    artifacts {
        archives jar
    }
}

tasks.register('sourcesJar', Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set('sources')
}

tasks.register('javadoc', Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

tasks.register('javadocJar', Jar) {
    dependsOn javadoc
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

ext {
    PUBLISH_ARTIFACT_ID = 'infobip-mobile-messaging-huawei-chat-sdk'
}
apply from: "${rootProject.projectDir}/publish-mavencentral.gradle"
apply from: "${rootProject.projectDir}/jacoco.gradle"