buildscript {
    repositories {
        google()
        mavenCentral()
        // huawei maven
        maven {url 'https://plugins.gradle.org/m2/'}
        maven { url 'https://developer.huawei.com/repo/' }
    }
    dependencies {
        classpath 'com.huawei.agconnect:agcp:1.6.0.300'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'com.huawei.agconnect'

android {
    compileSdkVersion 31
    buildToolsVersion mm_buildToolsVersion

    defaultConfig {
        minSdkVersion mm_minSdkVersion
        targetSdkVersion mm_targetSdkVersion
        versionCode 1
        versionName version
        applicationId "org.infobip.mobile.messaging.hmsdemo"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        debug {
            keyAlias 'hmsdemo'
            keyPassword '123456'
            storeFile file('src/HmsDemo.jks')
            storePassword '123456'
        }
        release {
            storeFile file("${property(project, 'signingKeystore', 'KEYSTORE')}")
            storePassword "${property(project, 'signingKeystorePassword', 'KEYSTORE_PASSWORD')}"
            keyAlias "alias_name"
            keyPassword "${property(project, 'signingKeystorePassword', 'KEYSTORE_PASSWORD')}"
            keyPassword "${project.hasProperty('signingKeystorePassword') ? project['signingKeystorePassword'] : System.getenv('KEYSTORE_PASSWORD')}"
        }
    }

    buildTypes {
        all {
            property(project, 'app_id', 'APP_ID')?.with { resValue "string", "app_id", delegate }
            property(project, 'infobip_application_code', 'INFOBIP_APPLICATION_CODE')?.with { resValue "string", "infobip_application_code", delegate }
            property(project, 'infobip_api_uri', 'INFOBIP_API_URI')?.with { resValue "string", "infobip_api_uri", delegate }
        }

        release {
            minifyEnabled true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        abortOnError false
    }

    flavorDimensions "default"

    productFlavors {
        push {
            dimension "default"
        }

        chat {
            dimension "default"
        }

        deeplink {
            dimension "default"
        }

        web {
            dimension "default"
        }

        customInApp {
            dimension "default"
        }

        cryptorMigration {
            dimension "default"
        }
        //Won't work, until we upgrade the huawei geolocation library
//        geofencing {
//            dimension "default"
//        }
        inbox {
            dimension "default"
        }

    }
}

dependencies {
    testImplementation "junit:junit:4.12"
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'

    implementation project(':infobip-mobile-messaging-huawei-chat-sdk')
// current version  of SDK doesn't support geo
//    implementation project(':infobip-mobile-messaging-huawei-geo-sdk')
    implementation project(':infobip-mobile-messaging-huawei-sdk')
    implementation project(':infobip-mobile-messaging-huawei-inbox-sdk')

    implementation "com.infobip:infobip-mobile-messaging-api-java:${mm_androidSdkVersion}"
    implementation ("com.infobip:infobip-mobile-messaging-android-resources:${mm_androidSdkVersion}@aar")

    // push kit
    implementation 'com.huawei.hms:push:6.3.0.302'

    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.0.0'

    //To add cryptor use build flavour "cryptorMigration"
    cryptorMigrationImplementation project(":infobip-mobile-messaging-huawei-cryptor-migration")
}

private static String property(Project project, String projectKey, String environmentKey) {
    return project.hasProperty(projectKey) ? project[projectKey] : System.getenv(environmentKey)
}