buildscript {
    repositories {
        jcenter()
        maven { url 'http://developer.huawei.com/repo/' }
        mavenCentral()
    }
    dependencies {
        classpath 'com.huawei.agconnect:agcp:1.2.1.301'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'com.huawei.agconnect'

android {
    compileSdkVersion 29
    buildToolsVersion mm_buildToolsVersion

    defaultConfig {
        minSdkVersion mm_minSdkVersion
        targetSdkVersion mm_targetSdkVersion
        versionCode 1
        versionName version
        applicationId "org.infobip.mobile.messaging.hmsdemo"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    signingConfigs {
        debug {
            keyAlias 'hmsdemo'
            keyPassword '123456'
            storeFile file('src/HmsDemo.jks')
            storePassword '123456'
        }
        release {
            storeFile file("${property(project, 'signingKeystore', 'KEYSTORE')}")
            storePassword "${property(project, 'signingKeystorePassword', 'KEYSTORE_PASSWORD')}"
            keyAlias "alias_name"
            keyPassword "${property(project, 'signingKeystorePassword', 'KEYSTORE_PASSWORD')}"
            keyPassword "${project.hasProperty('signingKeystorePassword') ? project['signingKeystorePassword'] : System.getenv('KEYSTORE_PASSWORD')}"
        }
    }

    buildTypes {
        all {
            property(project, 'app_id', 'APP_ID')?.with { resValue "string", "app_id", delegate }
            property(project, 'infobip_application_code', 'INFOBIP_APPLICATION_CODE')?.with { resValue "string", "infobip_application_code", delegate }
            property(project, 'infobip_api_uri', 'INFOBIP_API_URI')?.with { resValue "string", "infobip_api_uri", delegate }
        }
        release {
            minifyEnabled true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        abortOnError false
    }

    flavorDimensions "default"

    productFlavors {
        push {
            dimension "default"
        }

        chat {
            dimension "default"
        }

        deeplink {
            dimension "default"
        }

        web {
            dimension "default"
        }

        customInApp {
            dimension "default"
        }
    }
}

dependencies {
    testImplementation "junit:junit:4.12"
    androidTestImplementation "com.android.support.test:runner:1.0.2"

    implementation project(':infobip-mobile-messaging-huawei-chat-sdk')
    implementation project(':infobip-mobile-messaging-huawei-geo-sdk')
    implementation ('com.infobip:infobip-mobile-messaging-android-resources:5.2.0@aar')
    implementation project(':infobip-mobile-messaging-huawei-sdk')

    implementation 'com.infobip:infobip-mobile-messaging-api-java:5.2.0'

    implementation 'com.huawei.hms:push:5.0.0.300'

    implementation "com.android.support:appcompat-v7:${mm_supportLibrariesVersion}"
    implementation "com.android.support:design:${mm_supportLibrariesVersion}"
    implementation "com.android.support:support-v4:${mm_supportLibrariesVersion}"
}

private static String property(Project project, String projectKey, String environmentKey) {
    return project.hasProperty(projectKey) ? project[projectKey] : System.getenv(environmentKey)
}